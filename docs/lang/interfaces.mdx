# Interfaces

An **interface** is a set of required entrypoint messages that a contract must implement.
This allows a contract to be used in any context that expects a contract with a specific set of entrypoints.

For instance, a dApp can work with different CW20 token contracts without needing to know the specifics of each contract, as long as each contract implements the CW20 interface.

Contracts can implement multiple interfaces, as long as they implement all required entrypoints for each interface.
However, if two interfaces have conflicting signatures for the same entrypoint name, then a contract cannot implement both interfaces.

## Example

```cwscript
interface CW20 {
  // Interfaces may define custom types, but the implementing contract is not
  // obligated to use them.
  struct Coin {
    address: String,
    amount: U128
  }

  event Transfer(from: Address, to: Address, amount: Int)
  event Burn(from: Address, amount: Int)
  event Mint(to: Address, amount: Int)
  event Send(from: Address, to: Address, amount: Int)
  event IncreaseAllowance(owner: Address, spender: Address, amount: Int)
  event DecreaseAllowance(owner: Address, spender: Address, amount: Int)
  event TransferFrom(from: Address, to: Address, by: Address, amount: Int)
  event BurnFrom(from: Address, by: Address, amount: Int)
  event Log(from: Address, to: Address, by: Address, amount: Int)

  // Interfaces may not define any state items, as this would conflict with
  // the contract's own state items.

  // Typically, interfaces will not define any #instantiate entrypoints, as
  // this often conflicts with the contract's own instantiate entrypoint.
  // However, there are cases where it makes sense to define an #instantiate
  // structure for an interface, such as when the implementing contract is
  // expected to be instantiated by a factory contract that expects a specific
  // structure.

  // Required "exec" entrypoints -- the implementing contract must have all of
  // these entrypoints, with the exact same signatures matching in message name,
  // and each parameter's order, name, and type.
  exec #burn(amount: Int)
  exec #mint(recipient: String)
  exec #send(contract: String, amount: Int, msg: Binary)
  exec #increase_allowance(spender: String, amount: Int, expires?: Expiration)
  exec #decrease_allowance(spender: String, amount: Int, expires?: Expiration)
  exec #transfer_from(owner: String, recipient: String, amount: Int)
  exec #burn_from(owner: String, amount: Int)
  exec #send_from(owner: String, contract: String, amount: Int, msg: Binary)

  // Required "query" entrypoints -- same rules as "exec" entrypoints.
  query #balance(address: String) -> struct {
    balance: Int
  }

  query #token_info() -> struct TokenInfo {
    name: String,
    symbol: String,
    decimals: U8,
    total_supply: Int,
  }

  query #minter() -> MinterResponse?

}
```
