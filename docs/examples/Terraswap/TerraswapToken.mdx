# Token

```cwscript showLineNumbers
contract TerraswapToken implements CW20 {

  event Transfer(from: Address, to: Address, amount: Int)
  event Burn(from: Address, amount: Int)
  event Mint(to: Address, amount: Int)
  event Send(from: Address, to: Address, amount: Int)
  event IncreaseAllowance(owner: Address, spender: Address, amount: Int)
  event DecreaseAllowance(owner: Address, spender: Address, amount: Int)
  event TransferFrom(from: Address, to: Address, by: Address, amount: Int)
  event BurnFrom(from: Address, by: Address, amount: Int)
  event Log(from: Address, to: Address, by: Address, amount: Int)

  error InvalidZeroAmount()
  error Unauthorized()

  struct Timestamp(value: U64)

  enum Expiration {
    #Never
  	#AtTime(at: U64)
  	#AtHeight(at: Timestamp)
  }

  struct MinterData {
  	minter: Address,
	cap?: U128
  }

  struct TokenInfo {
     name: String,
     symbol: String,
     decimals: U8,
     total_supply: U128,
     mint?: MinterData
  }

  struct AllowanceResponse {
    allowance: U128,
	expires: Expiration
  }

  state {
    token_info: TokenInfo
    balances[owner: Address]: Int = 0
    allowances[owner: Address, spender: Address]: AllowanceResponse? = None
  }

  fn validate_accounts!(accounts: CW20.Coin[]) {
    let addresses = [];
  	for { address } in accounts {
  	  if (address in addresses) {
  	    fail! DuplicateInitialBalanceAddress(address);
  	  }
  	  addresses.push(address)
  	}
  }

  fn create_accounts!($state, accounts: CW20.Coin[]) -> Int {
    let total_supply = 0
    for row in accounts {
      let addr = Address!(row.address)
      $state.balances[addr] = row.amount
      total_supply += row.amount
    }
    return total_supply
  }

  #instantiate(
    name: String,
    symbol: String,
    decimals: U8,
    initial_balances: CW20.Coin[],
    mint?: MinterData
  ) {
    validate_accounts!(initial_balances)
    let total_supply = create_accounts!($state, initial_balances)
    if mint?.cap? and total_supply > mint.cap {
      fail! "Initial supply greater than cap";
    }
    if mint? {
	  let mint = MinterData(Address!(mint.minter), mint.cap)
    }
    $state.token_info = TokenInfo(name, symbol, decimals, total_supply, mint)
  }

  exec #transfer(recipient: String, amount: Int) {
    if amount == 0 {
      fail! InvalidZeroAmount();
    }

    let rcpt_addr = Address!(recipient)

    $state.balances[$info.sender] -= amount
    $state.balances[rcpt_addr] += amount

    emit Transfer($info.sender, Address!(recipient), amount)
  }

  exec #Burn(amount: Int) {
    if amount == 0 {
      fail! InvalidZeroAmount();
    }
    $state.balances[info.sender] -= amount
    $state.token_info.total_supply -= amount

    emit Burn($info.sender, amount)
  }

  exec #Mint(recipient: String, amount: Int) {
    if amount == 0 {
      fail! InvalidZeroAmount();
    }

    let config = $state.config

    if config.mint!.minter != $info.sender {
      fail! Unauthorized();
    }

    config.total_supply += amount
    let limit = config.cap

    if limit? and config.total_supply > limit {
      fail! CannotExceedCap();
    }

    $state.token_info = $state.config
    let rcpt_addr = Address!(recipient)
    $state.balances[rcpt_addr] += amount

    emit Mint(recipient, amount)
  }


  exec #send(contract: String, amount: Int, msg: Binary) {
    if amount == 0 {
      fail! InvalidZeroAmount();
    }

    let rcpt_addr = Address!(contract)
    $state.balances[$info.sender] -= amount
    $state.balances[rcpt_addr] += amount

    emit Send($info.sender, contract, amount)
  }

  exec #increase_allowance(
    spender: String,
    amount: Int,
    expires?: Expiration
  ) {

    let spender_addr = Address!(spender)
    if spender_addr == $info.sender {
      fail! CannotSetOwnAccount();
    }

    let allow = $state.allowances[env.sender, spender_addr]
    if expires? {
      allow.expires = expires
    }

    allow.allowance += amount
    $state.allowance[$info.sender, spender_addr] = allow
    emit IncreaseAllowance($info.sender, spender, amount)
  }

  exec #decrease_allowance(spender: String, amount: Int, expires?: Expiration) {
    let spender_addr = Address!(spender)
    if spender_addr == $info.sender {
      fail! CannotSetOwnAccount();
    }

    let allowance = $state.allowances[$info.sender, spender_addr]

    if amount < allowance.allowance {
      allowance.allowance -= amount
      if expires? {
        allowance.expires = expires
      }
      $state.allowances[$info.sender, spender_addr] = allowance
    } else {
      $state.allowances[$info.sender, spender_addr]::remove!()
    }

    emit DecreaseAllowance($info.sender, spender, amount)
  }

  exec #transfer_from(owner: String, recipient: String, amount: Int) {
    let rcpt_addr = Address!(recipient)
    let owner_addr = Address!(owner)

    deduct_allowance!(owner_addr, $info.sender, $env.block, amount)
    $state.balances[owner_addr] -= amount
    $state.balances[rcpt_addr] += amount

    emit TransferFrom(owner, recipient, $info.sender, amount)
  }

  exec #burn_from(owner: String, amount: Int) {
    let owner_addr = Address!(owner)
    deduct_allowance!(owner_addr, $info.sender, $env.block, amount)

    $state.balances[owner_addr]::update!(|s| s.checked_sub!(amount))
    $state.token_info.total_supply -= amount

    emit BurnFrom(owner, $info.sender, amount)
  }

  exec #send_from(owner: String, contract: String, amount: Int, msg: Binary) {
    let rcpt_addr = Address!(contract)
    let owner_addr = Address!(owner)

    deduct_allowance!(owner_addr, $info.sender, $env.block, amount)
    $state.balances[owner_addr] -= amount
    $state.balances[rcpt_addr] += amount

    emit SendFrom(owner, contract, $msg.sender, amount)
    exec! $.#send_from(sender, amount, msg)
  }

  query #balance(address: String) {
    let address = Address!(address)
    let balance = state.balances[address]

    return {
      balance
    }
  }

  query #token_info() {
    return state.token_info
  }

  query #minter() {
    return state.token_info.mint
  }

  query #allowance(owner: String, spender: String) {
    let owner_addr = Address!(owner)
    let spender_addr = Address!(spender)
    return $state.allowances[owner_addr, spender_addr]
  }

  query #all_allowances(owner: String, start_after?: String, limit?: Int) -> AllowanceResponse {
    let limit = (limit ?? DEFAULT_LIMIT).min(MAX_LIMIT)
    let allowances = $state.allowances[owner_addr]::range(start, None)
      .take(limit)
      .map(|item| item.map(|addr, allow| AllowanceInfo {
          spender: addr,
          allowance: allow.allowance,
          expires: allow.expires
       }))
    return {
      allowances
    }
  }
}
```
